import os, sys
import bldutil, rsf.doc

################################################################################

Import('env bindir pkgdir system user trip dotproj')

bldutil.py_install('__init__', env, pkgdir)
bldutil.py_install('__init__', env, os.path.join(pkgdir,'user'))

dirs = Split('plot libplot plplot pens sumain suplot') + system + user 

for usr in user:
    py = 'sf'+usr+'.py'
    env.InstallAs(os.path.join('.',py),
                  os.path.join('../../user',usr,py))

for sub in trip:
    name = os.path.basename(sub).replace('++','pp')
    dirs.append(name)
    py = 'sf'+name+'.py'
    env.InstallAs(os.path.join('.',py),
                  os.path.join('../..',sub,'main',py))

env.Command('prog.py',map(lambda x: 'sf'+x+'.py',dirs),
            action=Action(rsf.doc.getprogs))
env.Depends('prog.py',['vpplot.py','doc.py'])

env.Command('use.py',dotproj,action=Action(rsf.doc.use))
env.Depends('use.py',['prog.py','conf.py','doc.py'])

env.Command('version.py','#/VERSION.txt',
            action=Action(rsf.doc.getversion))
env.Depends('version.py','doc.py')

for src in Split('path doc flow proj prog tex book suproj conf use version node'):
    bldutil.py_install(src, env, pkgdir)

for src in Split('latex2wiki sftour'):
    bldutil.py_install(src, env, pkgdir)
    env.Command(src,src+'.py','cp $SOURCE $TARGET')
    env.Install(bindir,src)

env.Command('sfversion','version.py','cp $SOURCE $TARGET')
env.Install(bindir,'sfversion')

env.Install(bindir,'sfdoc')

SConscript(dirs='gui',name='SConscript',exports='env bindir pkgdir system user dotproj')

##########################################################################
# Self-documentation

Doc = Builder (action = Action(rsf.doc.selfdoc,varlist=['rsfprefix','lang','version']),
               src_suffix='.c',suffix='.py')
env.Append(BUILDERS = {'Doc' : Doc})
