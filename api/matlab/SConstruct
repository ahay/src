import sys, os, string, re
sys.path.append('../..')
import configure

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = configure.Debug()
    root = None
    SConscript('../c/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=['drsf'])

if 'matlab' in env.get('API',[]):
    mex = env.get('MEX')
    suffix = env.get('MEXSUFFIX')

    if mex:
        mexcom = mex + " CFLAGS='$CCFLAGS $_CCCOMCOM -fPIC' $SOURCE $_LIBDIRFLAGS $_LIBFLAGS -output $TARGET"

    for inp in Split('par dim read write create'):
        cfile = 'rsf_' + inp

        if mex:
            mfile = env.Command(cfile+suffix,cfile+'.c',mexcom)

            if root:
                install = env.Install(libdir,mfile)

                if env['PLATFORM'] == 'darwin':
                    env.AddPostAction(install,
                                      'install_name_tool -change '
                                      'build/api/c/libdrsf.dylib '
                                      '%s/libdrsf.dylib %s/%s' % \
                                          (libdir,libdir,mfile[0]))
