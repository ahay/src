import os, sys, string
sys.path.append('../../framework')
import bldutil

try:  # distributed version
    Import('env root pkgdir bindir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    srcroot = os.environ.get('RSFSRC', '../..')
    root = None
    SConscript('../../api/c/SConstruct')


# YOU MAY HAVE TO EDIT THIS SECTION!!!
# MPI_LIB and MPI_INC must be defined for your specific system
MPI_LIB = '/opt/openmpi/1.6.0/gcc/4.4.6/lib'		# MPI lib directory (contains libmpi.so)
MPI_INC = '/opt/openmpi/1.6.0/gcc/4.4.6/include'		# MPI include directory	(contains mpi.h)


mpi_err = 0

if not os.path.exists(MPI_LIB + "/libmpi.so"):
	mpi_err += 1

if not os.path.exists(MPI_INC + "/mpi.h"):
	mpi_err += 1


cuda_progs = '''
ewefd2d_gpu
ewefd3d_gpu_p2p
'''

cuda_mpi_progs = '''
ewefd3d_multiNode
'''


CTP = env.get('CUDA_TOOLKIT_PATH')
NVCC = env.get('NVCC')


env.Append(LIBS=[env.get('DYNLIB','')+'rsf'],
           CPPPATH=['../../include'],
           LIBPATH=['../../lib'])

if CTP:
    env.Append(LIBS=['cudart','cufft','cusparse'],
               LIBPATH=filter(os.path.isdir,[os.path.join(CTP,'lib64'),
                                             os.path.join(CTP,'lib')]))

cuda_mains = Split(cuda_progs)
for prog in cuda_mains:
    if CTP and NVCC:
        cfile = env.Command(prog+'.c','M'+prog+'.cu','cp $SOURCE $TARGET')
        cc = env.get('CC')
        cflags = '-prec-div=true -arch=sm_21 --x=cu'
        ldflags = ''
        if string.rfind(cc,'icc') >= 0:
            cflags = cflags + ' -ccbin=%s' % cc
            ldflags = '-ccbin=%s' % cc
        prog = env.Program(cfile,
                           CC=NVCC,
                           LINKFLAGS=ldflags,
                           CFLAGS=cflags)
    else:
        prog = env.RSF_Place('sf'+prog,None,
                             message='''
                             Please ensure CUDA is installed correctly.
                             Rerun Madagascar installation after it has been fixed.
                             ''')
    if root:
        env.Install(bindir,prog)


cuda_mpi_mains = Split(cuda_mpi_progs)
for prog in cuda_mpi_mains:
    if CTP and NVCC and (mpi_err == 0):
        cfile = env.Command(prog+'.c','M'+prog+'.cu','cp $SOURCE $TARGET')
        prog = env.Program(cfile,
                           CC=NVCC,
                           LINKFLAGS='-L'+MPI_LIB+' -lmpi',
                           CFLAGS='-I'+MPI_INC+' -prec-div=true -arch=sm_21 --x=cu')
    else:
        prog = env.RSF_Place('sf'+prog,None,
                             message='''
                             Please ensure CUDA and MPI are installed correctly.
                             Install script was unable to locate your mpi lib and/or include directory
                             Rerun Madagascar installation after it has been fixed.
                             ''')
    if root:
        env.Install(bindir,prog)


######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user

    docs = map(lambda prog: env.Doc(prog,'M' + prog+'.cu'),cuda_mains + cuda_mpi_mains)
    env.Depends(docs,'#/framework/rsf/doc.py')	
    
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)

