import os, sys, re, string
sys.path.append('../../framework')
import bldutil

progs = '''
helm2D_forward
helm2D_bornsyn
helm2D_genshot
helm2D_genrec
helm2D_rtm
helm2D_lsm
helm2D_fwi
'''
# fwiobj fwiupdate fwidir fwigrad iwinlcg0 

try:  # distributed version
    Import('env root pkgdir bindir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../pwd/SConstruct')

src = Glob('[a-z]*.c')

sparsepath = env.get('SPARSEPATH')
sparselibs = env.get('SPARSELIBS')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=[env.get('DYNLIB','')+'rsfpwd',
                  env.get('DYNLIB','')+'rsf'])

# a bug in Apple's gcc
if env.get('CC').rfind('gcc') >= 0 and env['PLATFORM'] == 'darwin':
    env.Append(CPPDEFINES=['_FORTIFY_SOURCE=0'])
    
if sparsepath and sparselibs:
    env.Prepend(CPPPATH=sparsepath,
                LIBS=sparselibs)
    
for source in src:
    inc = env.RSF_Include(source,prefix='')
    env.Ignore(inc,inc)
    if sparselibs:
        obj = env.StaticObject(source)
        env.Depends(obj,inc)

mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    bldutil.depends(env,sources,'M'+prog)

    if sparsepath and sparselibs:
        prog = env.Program(prog,[x + '.c' for x in sources])
    else:
        prog = env.RSF_Place('sf'+prog,None,var='SPARSELIBS',
                             package='suitesparse')
        
    if root:
        env.Install(bindir,prog)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user
    
    docs = [env.Doc(prog,'M' + prog) for prog in mains]
    env.Depends(docs,'#/framework/rsf/doc.py')	
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
