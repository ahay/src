import os, sys, re, string
sys.path.append('../../framework')
import bldutil

progs = '''
abalance analytical angle angle2 approx arrival bdix bil1 bilat2 blur
cfftwave1 cdivn cflow chebvc clpf cltft constperm constpermh
cosftwave1 cpef deblur distance divn dix dpeiko eikonal eikonalvti
eno2 erf exgr fft2 fftexp0 fftexp1 fftexp3 fftone ffttest fftwave1
fftwave2 fftwave3 focus fpow freqest gaussmooth gbeamform imray interf
interp2 interpt iphase kron lfftexp0 localskew locov lpf lsfit median
mffit mig3 miss3 nconv nsmooth nsmooth1 ocparcel octentwt odip2 ofilp
ofsemb opwd2 ortho patch phaserot pick pick3 plane poly polyfit regr
rdiv rect1 reshape riesz rsin seislet1 semblance shape shapeagc
shapefill shearer shift1 similarity similarity2 simenv smoothderw
smoothreg smspray taupfit thin timecont timefreq tomo tree trace2
tristack tristack2 twofreq2 var2 velcon velinv vidattr vofzperm warp1
warpadd warpscan zero zmarch ztrace
'''

ccprogs = 'anisolr2 isolr2 isolr3 permlr1 permlr2 permlr3'

pyprogs = 'cgi fft ipick las2rsf results wxresults zoom'
pymods = 'conjdir conjgrad dottest steepd'

try:  # distributed version
    Import('env root pkgdir bindir libdir incdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None

    SConscript('../lexing/SConstruct')

src = Glob('[a-z]*.c')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'])

fftw = env.get('FFTW')
if fftw:
    env.Prepend(CPPDEFINES=['SF_HAS_FFTW'])

dynpre = env.get('DYNLIB','') 

libs = [dynpre+'rsf']+env.get('LIBS',[])
dlibs = ['drsf']+env.get('LIBS',[])

objects = []
dobjects = []
includes = []
for source in src:
    inc = env.RSF_Include(source,prefix='')
    obj = env.StaticObject(source)
    env.Depends(obj,inc)
        
mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    bldutil.depends(env,sources,'M'+prog)
    env.StaticObject('M'+prog+'.c')
    prog = env.Program(prog,map(lambda x: x + '.o',sources),LIBS=libs)
    if root:
        install = env.Install(bindir,prog)

        if dynpre and env['PLATFORM'] == 'darwin':
            env.AddPostAction(install,
            '%s -change build/api/c/libdrsf.dylib '
            '%s/libdrsf.dylib %s' % \
                (WhereIs('install_name_tool'),libdir,install[0]))

if 'c++' in env.get('API',[]):
    lapack = env.get('LAPACK')
else:
    lapack = None

if lapack:
    libsxx = [dynpre+'rsf++','vecmatop']
    if not isinstance(lapack,bool):
        libsxx.extend(lapack)
    libsxx.extend(libs)

ccmains = Split(ccprogs)
for prog in ccmains:
    sources = ['M' + prog]
    if lapack:
        prog = env.Program(prog,map(lambda x: x + '.cc',sources),LIBS=libsxx)
        if root:
            install = env.Install(bindir,prog)
            
            if dynpre and env['PLATFORM'] == 'darwin':
                env.AddPostAction(install,
                '%s -change build/api/c/libdrsf.dylib '
                '%s/libdrsf.dylib %s' % \
                (WhereIs('install_name_tool'),libdir,install[0]))
                env.AddPostAction(install,
                '%s -change build/api/c++/libdrsf++.dylib '
                '%s/libdrsf++.dylib %s' % \
                (WhereIs('install_name_tool'),libdir,install[0]))
    else:
        prog = env.RSF_Place('sf'+prog,None,var='LAPACK',package='lapack')
        if root:
            env.Install(bindir,prog)

if 'c++' in env.get('API',[]):
    prog = env.Program('cerf',map(lambda x: x + '.cc',['Mcerf','faddeeva']),LIBS=[dynpre+'rsf++']+libs)
    env.Depends('cerf','faddeeva.hh')
    if root:
        install = env.Install(bindir,prog)
            
        if dynpre and env['PLATFORM'] == 'darwin':
            env.AddPostAction(install,
                              '%s -change build/api/c/libdrsf.dylib '
                              '%s/libdrsf.dylib %s' % \
                                  (WhereIs('install_name_tool'),libdir,install[0]))
            env.AddPostAction(install,
                              '%s -change build/api/c++/libdrsf++.dylib '
                              '%s/libdrsf++.dylib %s' % \
                                  (WhereIs('install_name_tool'),libdir,install[0]))
else:
    prog = env.RSF_Place('sfcerf',None,package='c++')
    if root:
        env.Install(bindir,prog)

jpeg = env.get('JPEG')
if jpeg:
    jpeg_o = env.StaticObject('_jpeg.c')
    env.Depends(jpeg_o,'_jpeg.h')

for conv in ('byte2jpg','jpg2byte'):
    if jpeg:
        prog = env.Program('sf'+conv,['_%s.c' % conv,jpeg_o],LIBS=libs+[jpeg])
    else:
        prog = env.RSF_Place('sf'+conv,None,var='JPEG',
                             package='libjpeg-devel')
    if root:
        env.Install(bindir,prog)

tiff = env.get('TIFF')

for conv in ('byte2tif','tif2byte'):
    if tiff:
        prog = env.Program('sf'+conv,'_%s.c' % conv,LIBS=libs+[tiff])
    else:
        prog = env.RSF_Place('sf'+conv,None,var='TIFF',package='libtiff-devel')
    if root:
        env.Install(bindir,prog)

for prog in Split('agrid burg cburg grid1 ntriangle1 smspray spray'):
    sources = ['Test' + prog,prog]
    bldutil.depends(env,sources,prog)
    sources = map(lambda x: x + '.o',sources)
    env.Object('Test' + prog + '.c')
    env.Program(sources,PROGPREFIX='',PROGSUFFIX='.x',LIBS=libs)

######################################################################
# PYTHON METAPROGRAMS (python API not needed)
######################################################################

if root: # no compilation, just rename
    pymains = Split(pyprogs)
    exe = env.get('PROGSUFFIX','')
    for prog in pymains:
        env.InstallAs(os.path.join(bindir,'sf'+prog+exe),'M'+prog+'.py')
    for mod in Split(pymods):
        env.Install(pkgdir,mod+'.py')

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user
    
    docs = map(lambda prog: env.Doc(prog,'M' + prog),mains) +  \
           map(lambda prog: env.Doc(prog,'M%s.cc' % prog,lang='c++'),ccmains+['cerf']) + \
           map(lambda prog: env.Doc(prog,'_' + prog),['byte2jpg','jpg2byte']) + \
           map(lambda prog: env.Doc(prog,'_' + prog),['byte2tif','tif2byte']) + \
           map(lambda prog: env.Doc(prog,'M'+prog+'.py',lang='python'),pymains)
    env.Depends(docs,'#/framework/rsf/doc.py')	

    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
