import os, sys, re, string, glob
sys.path.append('../../framework')
import bldutil

progs = '''
anisodiffuse anisodiffuse2 azpwd azspr diffuse2 gpi3dzo linmig2 linmig3 linpi linpipwd2d lspiazpwdmig3 lspiazpwdmig32 mute piazpwdmig3 pipwdmig2 psovc psovcp pwddiffuse pwdtensorh
'''

mpi_progs = ''

ccprogs = 'cerf'

subs = 'Faddeeva3'

try:  # distributed version
    Import('env root pkgdir bindir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../../api/c++/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=[env.get('DYNLIB','')+'rsf'])

if 'c++' in env.get('API',[]):
    env.Prepend(LIBS=[env.get('DYNLIB','')+'rsf++'])

src = Glob('[a-z]*.c')

for source in src:
    inc = env.RSF_Include(source,prefix='')
    obj = env.StaticObject(source)
    env.Ignore(inc,inc)
    env.Depends(obj,inc)

mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    bldutil.depends (env, sources, 'M'+prog)
    prog = env.Program (prog, [x + '.c' for x in sources])
    if root:
        env.Install (bindir, prog)

ccmains = Split(ccprogs)
subs = Split(subs)

if 'c++' in env.get('API',[]):
    objs = [env.StaticObject(x+'.cc') for x in subs]

for prog in ccmains:
    if 'c++' in env.get('API',[]):
        prog = env.Program (prog, ['M'+prog+'.cc'] + objs)
        env.Depends(prog,[x + '.hh' for x in subs])
    else:
        prog = env.RSF_Place ('sf' + prog, None, var='API', package='c++')
    if root:
        env.Install(bindir,prog)


for prog in Split('t2warp'):
    sources = ['Test' + prog,prog]
    bldutil.depends(env,sources,prog)
    sources = [x + '.o' for x in sources]
    env.Object('Test' + prog + '.c')
    env.Program(sources,PROGPREFIX='',PROGSUFFIX='.x')     

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user

    docs = [env.Doc(prog,'M' + prog) for prog in mains] + \
           [env.Doc(prog,'M%s.cc' % prog, lang='c') for prog in ccmains]
    env.Depends(docs,'#/framework/rsf/doc.py')
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
