import os, sys, string
sys.path.append('../../framework')
import bldutil

progs = '''
radon2 radon3 radon32 apradon2 radon34 diradon2 diiradon2 adiradon2 diradon32 diradon3 diradon34 sum sum3 sthres 
'''

subs = '''
bfio bfio_eval2 bfio_eval3 bfio_apeval2 bfio_eval34
'''

#added by XZ

cprogs = '''
nmo3_ort inmo3_ort
'''

try:  # distributed version
    Import('env root bindir pkgdir incdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    #    SConscript('../../api/c++/SConstruct')
    SConscript('../lexing/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=[env.get('DYNLIB','')+'rsf'])

##added by XZ
ccinc = Glob('[a-z]*.h*')

src = Glob('[a-z]*.c')

for source in src:
    inc = env.RSF_Include(source,prefix='')
    obj = env.StaticObject(source)
    env.Depends(obj,inc)


if 'c++' in env.get('API',[]):
    lapack = env.get('LAPACK')
else:
    lapack = None

if lapack:
    env.Prepend(LIBS=['vecmatop',
                      env.get('DYNLIB','')+'rsf++'])
    if not isinstance(lapack,bool):
        env.Append(LIBS=lapack)

    ccfiles = map(lambda x: x+'.cc',Split(subs))

    lib = env.StaticLibrary('bfio',ccfiles,CCFLAGS='')
    env.Install('../../lib',lib)
    env.Install('../../include',ccinc)


mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    if lapack:
        prog = env.Program(prog,map(lambda x: x + '.cc',sources)+[lib])
        env.Depends(prog,ccinc)
    else:
        prog = env.RSF_Place('sf'+prog,None,var='LAPACK',package='lapack')
    if root:
        env.Install(bindir,prog)
        env.Install(incdir,'bfio.bin')

cmains = Split(cprogs)
for prog in cmains:
    sources = ['M' + prog]
    bldutil.depends (env, sources, 'M'+prog)
    prog = env.Program (prog, map (lambda x: x + '.c',sources))
    if root:
        env.Install (bindir, prog)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user
    
#   docs = map(lambda prog: env.Doc(prog,'M%s.cc' % prog,lang='c++'),
#              Split(progs))
    docs = map(lambda prog: env.Doc(prog,'M' + prog),cmains) + \
           map(lambda prog: env.Doc(prog,'M%s.cc' % prog, lang='c++'),mains)
    env.Depends(docs,'#/framework/rsf/doc.py')
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
#####################################################################
