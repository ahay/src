import sys, os
sys.path.append('../../framework')
import bldutil

progs = '''
integral1 pseudodepth zomiso zomvti
'''

mpi_progs = '''
mpirtmiso
'''

place = ''

try:  # distributed version
    Import('env root pkgdir bindir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../../api/c/SConstruct')

src = Glob('[a-z]*.c')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=[env.get('DYNLIB','')+'rsf'])

mpicc = env.get('MPICC')
for source in src:
    inc = env.RSF_Include(source,prefix='')
    obj = env.StaticObject(source)
    env.Ignore(inc,inc)
    env.Depends(obj,inc)

lapack = env.get('LAPACK')
if lapack:
    if not isinstance(lapack,bool):
        env.Prepend(LIBS=lapack)
    progs += ' pseudodepth'
elif root:
    place += ' pseudodepth'
    for prog in Split('pseudodepth'):
        prog = env.RSF_Place('sf'+prog,None,var='LAPACK',package='lapack')     
        env.Install(bindir,prog)

fftw = env.get('FFTW')
if fftw:
    progs += ' zomiso zomvti'
elif root:
    place += ' zomiso zomvti'
    for prog in Split('zomiso zomvti'):
        prog = env.RSF_Place('sf'+prog,None,var='FFTW',package='fftw')
        env.Install(bindir,prog)

mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    bldutil.depends(env,sources,'M'+prog)
    env.StaticObject('M'+prog+'.c')
    prog = env.Program(prog,[x + '.o' for x in sources])
    if root:
        env.Install(bindir,prog)

mpi_mains = Split(mpi_progs)
if mpicc and fftw:
    for prog in mpi_mains:
        sources = ['M' + prog]
        bldutil.depends(env,sources,'M'+prog)
        env.StaticObject('M'+prog+'.c',CC=mpicc)
        prog = env.Program(prog,[x + '.o' for x in sources],CC=mpicc)
        if root:
            env.Install(bindir,prog)
else:
    place += mpi_progs
    for prog in mpi_mains:
        prog = env.RSF_Place('sf'+prog,None,var='MPICC',package='mpi')
        if root:
            env.Install(bindir,prog)


######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user
    
    docs = [env.Doc(prog,'M' + prog) for prog in mains+mpi_mains+Split(place)]
    env.Depends(docs,'#/framework/rsf/doc.py')	
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
